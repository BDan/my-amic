;         **************
;         *    AMIC    *
;         **************
;
;      MONITOR PENTRU CALCULATORUL PERSONAL AMIC
	CPU	8080
	ORG   0
	MVI   A,92H ;CUVINT CDA 8255
	OUT   CWPPI  ;INSCRIERE IN 8255
	XRA   A
	JMP   ET46
	SHLD  STVUT+6       ;REFACE H,L
	PUSH  PSW
	POP   H
	SHLD  STVUT         ;REFACE PSW
	POP   H
	DCX   H
	SHLD  STVUT+10      ;SALVEAZA PC
	LDA   MONSP-1       ;REFACE OCTET PROGRAM
	MOV   M,A
	LXI   H,0
	DAD   SP
	SHLD  STVUT+8       ;REFACE SP
	LXI    SP,STVUT+6
	PUSH  D             ;REFACE D.E
	PUSH  B             ;REFACE B,C
	LXI   SP,MONSP
	CALL  INVHL          ;INVERSEAZA ORDINEA
	JMP   BUCLA
ET46:  LXI   H,RND
	MVI   C,50
;SECVENTA DE INITIALIZARE CU ZERO 
ET45:  MOV   M,A
	INX   H
	DCR   C
	JNZ   ET45
	LXI   D,USESP ;STIVA UTILIZATOR
	MOV   H,E    ;IN ZONA REG. UTILIZATOR
	MOV   L,D
	SHLD  STVUT+8
START: LXI   SP,MONSP      ;INITIALIZARE SP MONITOR
	CALL  INITV          ;STERGE ECRAN
	LXI   H,TEXT1 ;H,L=ADRESA INCEPUT TEXT
	MVI    B,04  ;B=CONTOR CARACTERE
	CALL  TEXT   ;MESAJ MONITOR
BUCLA: CALL  CRLF
	MVI   C,'.'  ;PROMPTER
	CALL  AFIS
CMDR:  LXI   H,CDA
	MVI     B,17 ;NR MAX CARACTERE
ET42:  CALL  KEYIN  ;CITIRE TASTA
	CPI   DEL    ;ESTE TASTA DELETE?
	JZ    ET41   ;AFISARE
	JMP   ET43
; TRATARE INTRERUPERE NEMASCABILA
; RESTART ADRESA 0066H
; LA 66H SE GASESTE OCTET SUPERIOR DE ADRESA
; PENTRU JMP ET43	00H=NOP
	SHLD	STVUT+6	;H,L
	PUSH	PSW
	POP	H	;PSW
	SHLD	STVUT	;PSW
	POP	H
	SHLD	STVUT+10	;PC
	LXI	H,0
	DAD	SP
	SHLD	STVUT+8	;SP
	LXI	SP,STVUT+6
	PUSH	D	;D,E
	PUSH	B	;B,C
	LXI	SP,MONSP
	CALL	INVHL	;INVERSEAZA ORDINEA
	JMP	BUCLA
ET43:	MOV	C,A
	MOV	M,A	;INTRODUCERE IN BUFFER COD TASTA APASATA
	CALL	AFIS	;AFISARE
	INX	H
	DCR	B
	JZ	ERR	;CDA PREA LUNGA
	CPI	RETUR	;ESTE RETURN
	JNZ	ET42
	MVI	C,0AH	;TIPARESTE LF
	CALL	AFIS
	LXI	D,CDA
	LDAX	D
	INX	D
	CPI	'D'
	JZ	DISP
	CPI	'S'
	JZ	SUBST
	CPI	'X'
	JZ	EXAM
	CPI	'C'
	JZ	CHNG
	CPI	'M'
	JZ	MOVE
	CPI	'F'
	JZ	FILL
	CPI	'G'
	JZ	GO
	CPI	'L'
	JZ	LOAD
	CPI	'K'
	JZ	STORE
	CPI	'B'
	JZ	BASIC
ERR:	MVI	C,'?'	;;COD CDA ANULATA
	CALL	AFIS
	JMP	BUCLA
; SECVENTA DELETE
ET41:	LDA	COL
	CPI	1
	JZ	ET42
	DCR	A
	STA	COL
	DCX	H
	INR	B
	JMP	ET42
;
;----------------- 	
;SUBRUTINA KEY
;----------------	
;SCANEAZA TASTATURA
; C0-C2 =NR LINIE SCANARE
; AI = 1 , LINIA I DE RETURN
; Z=1/0 DA/NU TASTA APASATA
;
KEY:	LDA	EINV
	ANI	0F9H
	MOV	C,A	;SALVARE C
KEY1:	OUT	PORTC	;INSCRIE VAL SCANARE IN PORTC
	IN	PORTA	;CITESTE RETURN
	XRI	0FFH	;COMPLEMENTEAZA
	RNZ	;DACA <>0 ,TASTA APASATA Z=0
	INR	C
	MOV	A,C
	ANI	7
	JNZ	KEY1
	RET
;
;----------------	
;SUBRUTINA KEYIN
;----------------	
; CITESTE UN CARACTER DE LA TASTATURA
; A=COD ASCII AL TASTEI APASATE
;
KEYIN: PUSH	H
	PUSH	D
	PUSH	B	;SALVARE REGISTRE
ET3:	MVI	B,F0H	;ASTEAPTA !
	CALL	BITW
	MVI	A,5FH	;CURSOR
	CALL	WRITE
	MVI	B,F0H
	CALL	BITW
	MVI	A,20H
	CALL	WRITE
	CALL	KEY
	JZ	ET3	;RELUARE DACA NU TASTA APASATA
ET2:	MOV	B,A	;SALVARE
	LXI	H,TAB	;ADRESA TABELEI DE CODURI
	IN	PORTB
	ANI	20H	;TEST BIT S=SHIFT
	JNZ	ET10
	LXI	D,64
	DAD	D
ET10:	MOV	A,C
	LXI	D,8
	ANI	7
	JZ	ET11
ET5:	DAD	D
	DCR	A
	JNZ	ET5
ET11:	MOV	A,B
ET7:	RRC
	JC	ET6
	INX	H
	JMP	ET7
ET6:	IN	PORTB
	ANI	40H	;TEST PB6=CTRL
	MOV	A,M	;CITESTE COD IN A
	JNZ	ET8	;REVENIRE DACA NON CTRL
	ANI	1FH	;DACA CTRL,ANULEAZA BITII 6 ,5
ET8:	MOV	H,A	;SALVARE COD
ET9:	CALL	KEY	;ASTEAPTA ELIBERARE TASTA APASATA
	JNZ	ET9
	MOV	A,H
	CALL	BIP
	POP	B
	POP	D
	POP	H
	RET
;
;--------------	
;SUBRUTINA BIP
;--------------		
;MARTOR SONOR APASARE TASTA
;
BIP:	PUSH	PSW
	PUSH	B
	MVI	C,10H	;NR. IMPULSURI IN DIFUZOR
BIP1:	LDA	EINV
	PUSH	PSW
	MOV	B,A
	OUT	PORTC
	CALL	BITW
	POP	PSW
	XRI	8
	OUT	PORTC
	CALL	BITW
	DCR	C
	JNZ	BIP1
	POP	B
	POP	PSW
	RET
;
;--------------	
;SUBRUTINA WRITE
;--------------		
;AFISEAZA UN CARACTER LA CONSOLA
;NU ACTUALIZIEAZA POZITIE CURSOR
;A=CARACTER DE AFISAT
;20H<=CAR<=60H
;61H<=CAR SEMIGRAFIC<=70H
;
WRITE: PUSH	H
	PUSH	D
	PUSH	B
	CPI	61H	;A<61H,CY=1
	JC	WR27
	DCR	A
	MOV	C,A
	LXI	H,TAMP ;ADRESA TAMPON CAR. SEMIGRAFICE
	MVI	E,2	;NUMAR CICLURI
WR43:	ANI	1	;TEST BIT 0
	MVI	D,0FFH
	JZ	WR40
	MVI	D,0F0H
WR40:	MOV	A,C
	ANI	2	;TEST BIT 1
	JZ WR41
	MOV A,D	
	CMA
	MOV	D,A	;COMPLEMENTARE OCTET
WR41:	MVI	B,4
WR44:	MOV	M,D
	INX	H
	DCR	B
	JNZ	WR44
	MOV	A,C
	RRC
	RRC
	MOV	C,A
	DCR	E
	JNZ	WR40
	LXI	H, TAMP
	CALL   WR3O
	MVI	A,8	;CONTOR OCTETI
	CALL	WR26
	JMP	WR32
WR27:	LXI	H,CARGN ;ADRESA DE INCEPT GEN. CARACTERE
	LXI	D,6	;INCREMENT ADRESA
	SUI	20H
	JZ	WR20
WR21:	DAD	D 	;INCREMENTARE
	DCR	A	;DECREMENTARE COD
	JNZ	WR21	;ADRESA PT. GEN. CARACTER IN D,E
WR20:	CALL	WR3O
; BUCLA DE TIPARIRE 8 OCTETI PENTRU UN CARACTER
	LXI	B,20H
	LDA	VINV	;SEPARATORI RINDUR1
	CMA
	MOV	M,A
	DAD	B	;INCREMENTARE ADRESA PENTRU ECRAN
	MVI	A,6
	CALL	WR26
	LDA	VINV
	CMA
	MOV	M,A
WR32:	POP	B
	POP	D
	POP	H
	RET
;CALCULEAZA ADRESA PENTRU ECRAN
WR3O:	XCHG	;D,E=ADRESA IN GENERATORUL DE CARACTRE
	PUSH	D
	LXI	H,4001H ;APR. PRIMULUI CARACTER
	LXI	D,100H ;INCREMENT RIND CARACTER
	LDA	RND	;IN A SE AFLA NR. RIND
	ORA	A	;TEST A=0 ?
	JZ	WR22
WR23:	DAD	D	;INCR. ADR. RIND
	DCR	A
	JNZ	WR23
WR22:	LDA	COL	;IN A SE AFLA NR. COL.
	ORA	A
	JZ	WR24
WR25:	INX	H
	DCR	A
	JNZ	WR25	; H,L ADR. PT. MEM. ECRAN
WR24:	POP	D	; D,E ADR. PT. GEN. CARACTERE
	RET
WR26:	LXI	B,20H
	PUSH	PSW	;CONTOR DE OCTETI PT, UN CARACTER
	LDAX	D	;A=OCTET DIN GEN. CAR.
	CMA
	MOV	M,A	;TRIMITE PE ECRAN
	LDA	VINV
	XRA	M
	MOV	M,A
	DAD	B	;INCR. ADR. ECRAN
	INX	D	;INCR. ADR. GEN. CAR,
	POP	PSW
	DCR	A	;INCREMENTARE CONTOR
	JNZ	WR26
	RET
;
;---------------	
;SUBRUTINA SCROL
;---------------	
;REALIZEAZA DEFILARE ECRAN CU UN RIND DE CARACTERE
;
SCROL: LXI	H,4000H ;ADRESA DESTINATIE
	LXI	D,4100H		;ADRESA SURSA
SCR1:	LDAX	D
	MOV	M,A	;SCRIERE
	INX	D
	INX	H
	MOV	A,D
	CPI	60H	;STOP CIND D AJUNGE LA VAL 0A0H
	JNZ	SCR1
    LXI	H,5F00H ;SIFRGE
SCR2:	MVI	M,0FFH ;ULTIMUL RIND
	INX H
    MOV	A,H
	CPI	60H
	JNZ	SCR2
	RET
 ;
 ;---------------
 ;SUBRUTINA AFIS
 ;---------------
 ; SUBRUTINA DE AFISARE UN CARACTER
 ; C= CARACTER DE AFISAT
 ; INCREMENTEAZA POZITIE CURSOR
 ; EXECUTA SCROLL
 ;
AFIS: PUSH	H
	PUSH	D
	PUSH	PSW
	MOV	A,C
	CPI	0DH	;ESTE CR ?
	JNZ	AF1
	XRA	A
	STA	COL
	JMP	AF11
AF1:	CPI	0AH	;ESTE LF?
	JNZ	AF2
	LXI	H,RND
	INR	M
	MOV	A,M
	CPI	32
	JNZ	AF11
	DCR	M
	CALL	SCROL
	JMP	AF11
AF2:	CPI	5	;CTRL/E
	JNZ	AF3
	LDA	VINV	;VIDEO NORMAL / INVERS
	CMA	; 00 / FF
	STA	VINV
	JMP	AF11
AF3:	CPI	20H
	JC	AF11	;A<20H,CY=1,RETURN
	CPI	71H
	JNC	AF11	; A>=71H,CY=0,RETURN
	CALL	WRITE	;TIPARESTE
	LXI	H,COL
	INR		M
	MOV	A , M
	CPI	30
	JNZ	AF11
	XRA	A
	MOV	M,A	;INCREMENTEAZA POINTER RIND
	LXI	H,RND
	INR	M
	MOV	A,M
	CPI	32
	JNZ	AF11
	DCR	M
	LDA	AFMOD
	ORA	A
	JZ	AF12
	XRA	A
	STA	RND
	JMP	AF11
AF12:	CALL	SCROL
AF11:	POP	PSW
	POP	D
	POP	H
	RET
;
;----------------
;SUBRUTINA INITV
;----------------
; INITIALIZARE TV

INITV: LXI	H,4000H
ET19:	MVI	M,0FFH
	INX	H
	MOV	A,H
	CPI	60H
	JNZ	ET19
	RET
;
;----------------
;SUBRUTINA DISP
;----------------
; AFISEAZA O ZONA DE MEMORIE
; CUPRINSA INTRE ADRESA INFERIOARA
;  SI ADRESA SUPERIOARA
;
DISP: CALL	CONV2
	CPI	RETUR
	JNZ	ERR
;INCEPE AFISAREA
	XCHG	;ADRESA SFIRSIT ZONA - D,E
	LHLD	ADR1	;H,L=ADRESA INCPUT ZONA
ET51:	MOV	A,H	;ADRESA SUPERIOARA
	CALL	BINASC ;BINASC EXECUTA SI AFISAREA
	MOV	A,L	;ADRESA INFERIOARA
	CALL	BINASC ;CONVERSIE SI AFISARE
ET52:	CALL	AF20H
	MOV	A,M	;CIITIRE OCTET
	CALL	BINASC ;CONVERSIE SI TIPARIRE
	MOV	A,L
	CMP	E
	JNZ	ET50
	MOV	A,D
	CMP	H
	JZ	BUCLA
ET50:	INX	H
	MOV	A,L
	ANI	7
	JNZ	ET52
	CALL	CRLF
	JMP	ET51
;
;--------------------------
;SUBRUTINA SUBST
;--------------------------
; PERMITE AFISAREA SI MODIFICAREA
; UNUI SIR OF 0CTETI DIN MEMORIE
 ;
SUBST: CALL	CONVA	; H,L=ADRESA CURENTA
   		LDAX	D
	CPI	RETUR	;ESTE RETURN
	JNZ	ERR
ET64:	MOV	A,M	;CITESTE OCTET
	CALL	BINASC ;CONVERSIE SI TIPARIRE
	MVI	C,'-'
	CALL	AFIS
	LXI	D,CDA
	MVI	A,30H	;COD ASCII PT. ZERO
	STAX	D
	INX	D
	CALL	KEYIN	;CITESTE DE LA CONSOLA UN CARACTER
	CPI	RETUR	;DACA RETURN CDA INCHEIATA
	JZ	BUCLA
	CPI	20H	;DACA BLANC . OCTET URMATOR
	JZ	ET61
	MOV	C,A
	CALL	AFIS
ET63:	STAX	D	;I CAR IN BUF CDA
	CALL	KEYIN
	PUSH	PSW
	MOV	C,A	;AFISEAZA IN ECOU
	CALL	AFIS
	CPI	20H
	JZ	ET62
	CPI	RETUR
	JZ	ET62
	POP	PSW
	INX	D
	JMP	ET63
ET62:	DCX	D
	SHLD	ADR1	;SALVARE ADRFSA IN ZONA DE MEMORIE
	CALL	CONV1	;EGALARE NIV ADINCIME SUBRIUTINA
	MOV	A,L
	LHLD	ADR1	;REFACERE ADR1
	MOV	M,A	;MODIF OCTET IN MEMORIE
	POP	PSW
	CPI	RETUR
	JZ	BUCLA
ET61:	INX	H
	JMP	ET64
;
;---------------	
;SUBRUTINA CONV1
;---------------	
; EGALIZEAZA NIVELUL DE ADINCIME
; CONVA SI CONVB
;
CONV1: CALL	CONVB
	RET
;
;---------------	
;SUBRUTINA EXAM
;---------------	
;AFISEAZA REGISTRELE INTERNE UTILIZATOR
;
EXAM:	LDAX	D
	CPI	RETUR
	JNZ	ERR
	LXI	H,TEXT2
	MVI	B,27
	CALL	TEXT
	CALL	CRLF
	MVI	E,6
	LXI	H,STVUT
ET71:	MOV	A,M
	CALL	BINASC
	INX	H
	MOV	A,M
	CALL	BINASC
	INX	H
	DCR	E
	JZ	BUCLA
	CALL	AF20H
	JMP	ET71
;
;--------------
;SUBRUTINA CHNG
;--------------
; PERMITE AFISAREA SI MODIFICAREA
; REGISIRELOR UTILIZATOR
;
CHNG:	LDAX	D
	CPI	RETUR
	JNZ	ERR
	LXI	H,STVUT
	JMP	ET64
;
;-----------------
;SUBRUTINA MOVE
;-----------------
; MUTA O ZONA DE MEMORIE IN ALTA ZONA 
;
CONV2: CALL	CONVA
	SHLD	ADR1	;ADR. INCEPUT ZONA SURSA
	LDAX	D
	CPI	2CH
	JNZ	ER1
	INX	D
	CALL	CONVA
	SHLD	ADR2	;ADR. SFIRSIT ZONA SURSA
	LDAX	D
	RET
ER1:	POP	H
	JMP	ERR
MOVE:	CALL	CONV2
	CPI	2CH
	JNZ	ERR
	INX	D
	CALL	CONVA
	SHLD	ADR3	;ADR. INCEPUT ZONA DESTINATIE
	LDAX	D
	CPI	RETUR
	JNZ	ERR
;TRANSFER ZONA DE MEMORIE
	LHLD	ADR3
	XCHG	;D,E=ADRESA ZONA DESTINATIE
	LHLD	ADR1	;H,L=ADRESA ZONA SURSA
ET77:	MOV	A,M	;CITESTE DIN ZONA SURSA
	STAX	D	;SCRIE IN ZONA DESTINATIE
	LDA	ADR2
	CMP	L
	JNZ	ET75
	LDA	ADR2+1
	CMP	H
	JZ	BUCLA
ET75:	INX	H
	INX	D
	JMP	ET77
;
;-------------
;COMANDA FILL
;-------------
;UMPLE 0 ZONA DE MEMORIE CU 0 CONSTANTA
;
FILL:	CALL	CONV2
	CPI	2CH
	JNZ	ERR
	INX	D
	CALL	CONV1
	MOV	B,L 
	LHLD	ADR2
	XCHG	;D,E=ADRESA SUPERIOARA
	LHLD	ADR1	;H,L=ADRESA INFERIOARA
FIL1:	MOV	M,B	;INSCRIE OCTET
	INX	H
	MOV	A,L
	CMP	E
	JNZ	FIL1
	MOV	A,H
	CMP	D
	JNZ	FIL1
	MOV	M,B
	JMP	BUCLA
;
;-----------
;SUBRUTINA GO
;-----------
; LANSEAZA IN EXECUTIE UN PROGRAM UTILIZATOR
;
GO:	CALL	CONVA
	SHLD	ADR1
	LDAX	D
	CPI	RETUR
	JZ	ET81
	CPI	2CH	;VIRGULA
	JNZ	ERR
	INX	D
	CALL	CONVA
	LDAX	D
	CPI	RETUR
	JNZ	ERR
	MOV	A,M
	MVI	M,0CFH ;RST1
	PUSH	PSW	;SALVARE IN STIVA MONITOR OCTET PROGRAM
ET81:	CALL	INVHL	;INVERSARE OCTETI STIVA AFISARE
	LXI	SP,STVUT
	POP	PSW	;REFACERE REGISTRU UTILIZATOR
	POP	B
	POP	D
	POP	H
	POP	H
	SPHL	;INCARCARE SP UTILIZATOR
	LHLD	ADR1
	PUSH	H
	LHLD	STVUT+6 ;REFACERE H,L
	RET	;LANSARE PROGRAM
;
;	
;SUBRUTINA INVHL
;	
; INVERSEAZA ORDINEA OCTET LOW SI OCTET HIGH IN STVUT
;
INVHL: MVI	C,6
		LXI	H,STVUT
ET72:	MOV	A,M
	INX	H
	MOV	B,M
	MOV	M,A
	DCX	H
	MOV	M,B
	INX	H
	INX	H
	DCR	C
	JNZ	ET72
	RET
;
;---------------
;SUBRUTINA CONVA
;----------------
; CONVA=CONVERSIE ADRESA
; CONVERSIE 4 CARACTERE ASCII DE LA ADRESA DIN D,E(I CARECTER)
; ADRESA REZULTA IN H,L
; MODIFICA:A , AD , E=D , E+4
;
CONVA: LXI	H,0	;INITIALIZARE H,L
	CALL	CONVB ;REZULTAT IN L
	MOV	H,L
	CALL	CONVB
	RET
;
;	
;SUBRUTINA CONVB
;----------------
; CONVB=CONVERSIE BYTE
; 2 CARACTERE ASCII DE LA ADRESA DIN D,E 
; L=BYTE
; MODIFICA: A ,	D,E=D,E+2
;
CONVB: LDAX D
	CALL	ASCBIN
	CPI	10H	;TEST DACA CIFRA HEXA
	JNC	CON1
	RLC	;REZULTAT IN A0-3
	RLC
	RLC
	RLC
	MOV	L,A
	INX	D
	LDAX	D
	CALL	ASCBIN
	CPI	10H
	JNC	CON1
	ORA	L
	MOV	L,A
	INX D	;D,E ADRESA CARACTLR URMATOR DIN BUFFER CI
	RET
CON1:	POP D	;SCOATE DIN STIVA
	POP D	;DOUA ADRESE DE REVENIRE
	JMP	ERR
;
;----------------
;SUBRUTINA BINASC
;----------------
; EXECUTA AFISAREA
; MODIFICA B,C
; A=OCTET BINAR
;
BINASC: MOV	B,A	;SALVARE OCTET
	RRC
	RRC
	RRC
	RRC
	CALL	BN1
	MOV	A,B
	CALL	BN1
	RET
BN1:	ANI	0FH	;A,D0F0H ;SEPARARA CUARTET LOW
	CPI	10	;ADUCE CUARTET IN POZ A0-A3
	JC	BN2
	ADI	7
BN2:	ADI	'0'
	MOV	C,A
	CALL	AFIS
	RET
;
;	
;SUBRUTINA ASCBIN
;	
;	EXECUTA CONVERSIE ASCII=BINAR
;   A=OCTET ASCII
;	AO-A3=SEMIOCTET BINAR
;	MODIFICA : A
;
ASCBIN: SUI	30H
	CPI	10
	RC
	SUI	7
	RET
;	
;SUBRUTINA CRLF
;	
;CAP DE RIND . LINIE NOUA
;
CRLF:	MVI	C,0DH ;CR
	CALL	AFIS
	MVI	C,0AH ;LF
	CALL	AFIS
	RET
;----------------	
; SUBRUTINA TEXT
;----------------	
; TIPARESTE UN TEXT DIN MEMORIE
; H,L ADRESA DE INCEPUT ZONA TEXT
; B CONTOR DE CARACTERE
;
TEXT:	MOV	C,M	;CITESTE CARACTER
	CALL	AFIS
	INX H
	DCR B
	JNZ	TEXT
	RET
; SUBRUTINA AF2OH
; AFISEAZA BLANC LA DISPLAY
AF20H: MVI	C,20H
	CALL	AFIS
	RET
;
;-----------------
;SUBRUTINA STORE
;-----------------
;SALVEAZA PE CASETOFON O ZONA DE MEMORIE
;CUPRINSA INTRE ADRESELE ADR1 SI ADR2
;
STORE: CALL	CONV2
	CPI	RETUR
	JNZ	ERR
	XCHG	;D,E=ADRESA SUPERIOARA
	LHLD	ADR1
	MOV	A,L
	CMA
	MOV	L,A
	MOV	A,H
	CMA
	MOV	H,A
	INX H	;COMPLEMENTUL '2
	DAD D
	XCHG	;D,E=CONTOR
	LHLD	ADR1	;H,L=ADRESA INFERIOARA
	CALL	SRIOM
	JMP	BUCLA
SRIOM: PUSH	D
	LXI	D,0
PRAMB: MVI	B,30H
	CALL	IMPUL
	INX D
	MOV	A,D
	CPI	20H
	JNZ	PRAMB
	MVI	D,0AH
	CALL	IMPUL
	POP D
	MVI	C,0	;INITIALIZARE SUMA DE CONTROL
	MOV	A,H	;SCRIE ADRINF SI CONTOR DE OCTETI
	CALL	CKSMO
	MOV	A,L
	CALL	CKSMO
	MOV	A,D
	CALL	CKSMO
	MOV	A,E
	CALL	CKSMO
	DCX H
TAPE1: INX H
	MOV	A,M	;CITESTE OCTET
	CALL	CKSMO	;SALVEAZA—L PE CASETA
	MOV	A,D
	ORA E
	DCX D
	JNZ	TAPE1	;REIA PINA LA CONTOR NUL
	MOV	A,C	;SCRIE PE CASETA SUMA DE CONTROL
	CMA	;IN COMPLEMENT FATA DE 2
	INR	A
	CALL	CKSMO
 	CALL	CKSMO	;POSTAMBUL
	RET
;
;-----------
;SUBRUTINA LOAD
;--------------
;CITESTE UN FISIER DE LA CASETOFON
;SINTAXA: L(CR)
;CITESTE DE PE CASETA IN MEMORIE LA ADRESA
;CITITA DE PE CASETA
;
LOAD:	LDAX D
	CPI	RETUR
	JNZ	ERR
	CALL	LTAPE
	JMP	BUCLA
LTAPE: IN	21H
	MOV	B,A	
SRII1: IN	21H
	XRA B
	JZ	SRII1
SRII2: IN	21H
	ANI	1
	JNZ	SRII2
SRII3: IN	21H
	ANI	1
	JZ	SRII3
	CALL	BITR
	MVI	A,1DH	;ACC,B=DURATA IMPULS, CY=1
	CMP B
	JC	SRII3
	MVI	C,0	;SUMA DE CONTROL
	CALL	CKSMI	;CITESTE ADRINF SI CONTOR DE
	MOV	H,A	;PE CASETA
	CALL	CKSMI
	MOV	L,A
	CALL	CKSMI
	MOV	D,A
	CALL	CKSMI
	MOV	E,A
TAPE4: SHLD	ADR2	;PREGATESTE ZONA DE AFISAT
	XCHG
	INX H
	SHLD	ADR2-2
	DCX H
	XCHG	;D,E=ADR SUP -ADR INF
	DCX H	;H,L=ADRESA DE MEMORIE
TAPE2: INX H
	CALL	CKSMI	;CITESTE OCTET
	MOV	M,A
	MOV	A,D
	ORA	E
	DCX	D
	JNZ	TAPE2	;REIA PINA LA CONTOR NUL
	CALL	CKSMI
	JZ	ERG	;SALT LA TERMINARE NORMALA
	POP	PSW	;ACTUALIZEAZA SP
	JMP  ERR	 ;EROARE
	RET
ERG:	MVI	D,2	;AFISEAZA ADRINF SI CONTOR LA
	LXI	H,ADR2+1	;TERMINARE NORMALA
LMES:	MOV	A,M
	CALL BINASC 
	DCX	H
	MOV	A, M
	CALL	BINASC
	DCX	H
	CALL	CRLF
	DCR	D
	JNZ	LMES
	RET
;CALCULEAZA SUMA DE CONTROL LA SCRIERE
CKSMO: PUSH	PSW
		ADD	C
		MOV	C,A
		POP	PSW
		CALL	SRIOT
		RET
;CALCULEAZA SUMA DE CONTROL LA CITIRE
CKSMI: CALL	SRIIN
		MOV	B,A
		ADD	C
		MOV	C,A
		MOV	A,B
		RET
;SCRIE OCTET PE CASETA
SRIOT: PUSH	D
		MVI	E,8	;CONTOR BITI
SRIO3: MOV	D,A	;SALVARE OCTET IN D
		ANI 80H	;PREIA BIT 7
		MVI B,0EH	;BIT=0
		JZ	SRIO2
		MVI	B,22H	;BIT=1
SRIO2: CALL	IMPUL
		MOV	A,D
		RLC
		DCR	E
		JNZ	SRIO3
		POP	D
		RET
IMPUL: PUSH	B
		MVI	A,0FFH
		OUT	22H
		CALL	BITW
		POP	B
		XRA	A
		OUT	22H
		CALL	BITW
		RET
;CITESTE OCTET DE PE CASETA
SRIIN: PUSH	D
		PUSH	B
		MVI	E,8	;CONTOR BIT!
		XRA	A
SRII7: RLC
		MOV	D,A
SRII4: IN	21H
		ANI	1
		JZ	SRII4
		CALL	BITR
		MVI	A,18H	 ;VALOARE DE PRAG
		CMP B
		JC	SRII5
		XRA	A
		JMP	SRII6
SRII5: MVI	A,1
SRII6: ORA D
		DCR E
		JNZ	SRII7
		POP	B
		POP D
		RET
BITR:	IN	21H
		MOV	C,A
		MVI	B,0	;CONTOR IMPULS
BITR1: INR B
		IN	21H
		XRA C
		JZ BITR1
		RET
BITW:	IN	21H
		XRA B
		DCR	B
		JNZ	BITW
		RET
;-----------------
;TABELA DE SIMBOLI
;-----------------
;
;TABELA DE CODURI ASCII PENTRU TASTATURA 
;SCAN/RETURN : 00,01,...,07,10,11,...
TAB:	DB	09H,'1234567890-=',8,20H,20H
		DB	20H,'QWERTYUIOP[', 5CH,0AH,7FH,20H
		DB 'ASDFGHJKL;', 27H, 1BH, 0DH, 20H, 20H, 20H
		DB 'ZXCVBNM,./', 20H, 20H
TEXT1:  DB 'AMIC'
		DB 09H, '!@#$%^&*()-+',8,20H,20H
		DB 20H,71H,77H,65H,72H,74H,79H,75H
		DB 69H,6FH, 70H, 5DH,21H,0AH,7FH,20H
		DB 61H, 73H, 64H, 66H, 67H, 68H, 6AH, 6BH
		DB 6CH, 3AH, 22H, 1BH, 0DH, 20H, 20H, 20H
		DB 7AH, 78H, 63H, 76H, 62H, 6EH, 6DH, 3CH
		DB 3EH, 3FH, 20H
; 
;-----------------------
;GENERATORUL DE CARACTERE
;-----------------------
; STRUCTURA CAR ESTE: 5*6 PCTE, INTR-O MATRICE DE 8*8
; PCTELE 1,7,8 DIN OCTET SINT FOLOSITE CA SEPARATOARE DE CAR
; LINIILE 1 SI 8 SINT FOLOSITE CA SEPARATORI DE LINII DE CAR
;
CARGN:	DB 0, 0, 0, 0, 0, 0	;BLANK
		DB 10H, 10H, 10H, 10H, 0, 10H	;!
		DB 0H, 28H, 0H, 0H, 0H, 0H		;"
		DB 0H, 28H, 7CH, 28H, 7CH, 28H	;#
		DB 10H, 38H, 50H, 38H, 14H, 38H	;$
		DB	0, 24H, 8, 10H, 24H, 0		;%
		DB 20H, 50H, 20H, 54H, 48H, 34H	;&
		DB 8, 10H,  0, 0, 0, 0 			;'
		DB 20H, 40H, 40H, 40H, 40H, 20H	;(
		DB 8, 4, 4, 4, 4, 8				;)
		DB 0, 10H,  54H, 38H, 54H, 10H		;*
		DB 0, 10H, 10H, 7CH, 10H, 10H		;+
		DB 0, 0, 0, 0, 8, 10H				;,
		DB 0, 0, 0, 7CH, 0, 0				;-
		DB 0, 0, 0, 0, 0, 10H				;.
		DB 4, 4, 8, 10H, 20H, 20H ;/
		DB 38H, 4CH, 54H, 54H, 64H, 38H ;0
		DB 10H, 30H, 50H, 10H, 10H, 38H ;1
		DB 18H, 24H, 8, 10H, 20H, 3CH ;2
		DB 38H, 4H, 18H, 4H, 4H, 38H ;3
		DB 0CH, 14H, 24H, 3CH, 4H, 4 ;4
		DB 3CH, 20H, 38H, 4H, 4H, 38H ;5
		DB 18H, 20H, 38H, 24H, 24H, 18H ;6
		DB 3CH, 4H, 8H, 10H, 20H, 20H ;7
		DB 18H, 24H, 18H, 24H, 24H, 18H ;8
		DB 18H, 24H, 1CH, 4H, 4H, 18H ;9
		DB 0, 10H, 0H, 10H, 0H, 0 ; :
		DB 0, 10H, 0H, 10H, 20H, 0 ; ;
		DB 0, 18H, 20H, 40H, 20H, 18H ;<
		DB 0, 0, 7CH, 0, 7CH, 0 ;=
		DB 0, 30H, 8, 4, 8, 30H ;>
		DB 18H, 24H, 8H, 10H, 0H, 10H ;
		DB 38H, 44H, 58H, 58H, 40H, 3CH ;@
		DB 10H, 28H, 44H, 7CH, 44H, 44H	;A
		DB 78H, 44H, 78H, 44H, 44H, 78H	;B
		DB 38H, 44H, 40H, 40H, 44H, 38H	;C 
		DB 78H, 44H, 44H, 44H, 44H, 78H	;D 
		DB 7CH, 40H, 7CH, 40H, 40H, 7CH	;E
		DB 7CH, 40H, 7CH, 40H, 40H, 40H ;F  
		DB 38H, 44H, 40H, 5CH, 44H, 38H	;G  
		DB 44H, 44H, 7CH, 44H, 44H, 44H	;H 
		DB 38H, 10H, 10H, 10H, 10H, 38H	;I
		DB 3CH, 08H, 08H, 08H, 48H, 30H ;J
		DB 48H, 50H, 60H, 50H, 48H, 44H ;K
		DB 40H, 40H, 40H, 40H, 40H, 7CH ;L
		DB 44H, 6CH, 54H, 44H, 44H, 44H ;M
		DB 44H, 64H, 54H, 4CH, 44H, 44H ;N
		DB 38H, 44H, 44H, 44H, 44H, 38H ;O
		DB 78H, 44H, 78H, 40H, 40H, 40H ;P
		DB 38H, 44H, 44H, 54H, 48H, 34H ;Q
		DB 78H, 44H, 78H, 50H, 48H, 44H ;R 
		DB 3CH, 40H, 38H, 4, 4, 78H ;S
		DB 7CH, 10H, 10H, 10H, 10H, 10H ;T
		DB 44H, 44H, 44H, 44H, 44H, 38H ;U
		DB 44H, 44H, 44H, 44H, 28H, 10H ;U
		DB 44H, 44H, 44H, 54H, 6CH, 44H ;W
		DB 44H, 28H, 10H, 10H, 28H, 44H ;X
		DB 44H, 28H, 10H, 10H, 10H, 10H ;Y
		DB 7CH, 04H, 18H, 30H, 40H, 7CH ;Z
		DB 60H, 40H, 40H, 40H, 40H, 60H ;[
		DB 20H, 20H, 10H, 8, 4, 4 ;SLASH LEFT
		DB 0CH, 4, 4, 4, 4, 0CH ;]
		DB 10H, 28H, 44H, 0, 0, 0	;CARETA
		DB 0, 0, 0, 0, 0, 7CH ;BARA JOS
TEXT2: DB 'A F  B C  D E  H L  SP PC'
;
; TABELA DE JUMP-URI LA ADRESE IMPORTANTE DIN MONITOR
; 
	ORG 07F4H
KOUT:	JMP SRIOM
KIN:	JMP LTAPE
COUT:	JMP AFIS
CIN:	JMP KEYIN
;
;ECHIVALARI SI REZERVARI DE MEMORIE
;	
CWPPI EQU 23H
ADRIN EQU 8002H ;ADRESA INCEPUT ECRAN
DEL EQU 7FH
RETUR EQU 0DH
BASIC EQU 0800H
PORTA EQU 20H
PORTB EQU 21H
PORTC EQU 22H
ADREC EQU 8000H 	;ADRESA ECRAN
USESP EQU 06100H	;STIVA UTILIZATOR
	ORG 06000H
RND: DS 1
COL: DS 1
AFMOD: DS 1
EINV: DS 1
VINV: DS 1
CDA: DS 17
STVUT:  DS 12
CONT: DS 1
ADR1: DS 2
ADR2: DS 2
ADR3: DS 2
TAMP: DS 8
	DS 40
MONSP: DS 1
	END	

  